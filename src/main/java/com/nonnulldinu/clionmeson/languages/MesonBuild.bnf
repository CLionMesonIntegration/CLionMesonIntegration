{
  parserClass="com.nonnulldinu.clionmeson.languages.parsers.MesonBuildParser"
  parserUtilClass="com.nonnulldinu.clionmeson.languages.parsers.MesonBuildParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="MesonBuild"
  psiImplClassSuffix="Impl"
  psiPackage="com.nonnulldinu.clionmeson.build.psi"
  psiImplPackage="com.nonnulldinu.clionmeson.build.psi.impl"

  elementTypeHolderClass="com.nonnulldinu.clionmeson.build.psi.MesonBuildTypes"
  elementTypeClass="com.nonnulldinu.clionmeson.build.elements.MesonBuildElement"
  tokenTypeClass="com.nonnulldinu.clionmeson.build.elements.MesonBuildToken"

//  psiImplUtilClass="com.nonnulldinu.clionmeson.build.psi.impl.PsiImplUtil"
  tokens=[
    newline="regexp:\s*\R\s*"
    SPACES="regexp:[ \t]+"

    IN_OP="in"

    BRACE_BEGIN='['
    BRACE_END=']'
    CURLY_BRACE_BEGIN='{'
    CURLY_BRACE_END='}'
    PAREN_BEGIN='('
    PAREN_END=')'
    COMMA=','

    COMP_OP1='=='
    COMP_OP2='<='
    COMP_OP3='>='
    COMP_OP4='<'
    COMP_OP5='>'
    COMP_OP6='!='

    COMP_ATR_OP1='*='
    COMP_ATR_OP2='/='
    COMP_ATR_OP3='%='
    COMP_ATR_OP4='+='
    COMP_ATR_OP5='-='
    ATR_OP='='

    PLUS_OP='+'
    MINUS_OP='-'
    MULT_OP='*'
    DIV_OP='/'
    MOD_OP='%'

    BOOL_LITERAL_TRUE='true'
    BOOL_LITERAL_FALSE='false'

    LANG_TOKEN_FOREACH='foreach'
    LANG_TOKEN_FOREACH_END='endforeach'
    LANG_TOKEN_JUMP_CONTINUE='continue'
    LANG_TOKEN_JUMP_BREAK='break'
    LANG_TOKEN_IF='if'
    LANG_TOKEN_ELSE_IF='elif'
    LANG_TOKEN_ELSE='else'
    LANG_TOKEN_ENDIF='endif'

    LANG_TOKEN_AND='and'
    LANG_TOKEN_OR='or'
    LANG_TOKEN_NOT='not'

    LANG_TOKEN_QMARK='?'
    LANG_TOKEN_COLON=':'

    LANG_TOKEN_DOT='.'

    binnum='regexp:0b[01]+'
    hexnum='regexp:0x[a-fA-F0-9]+'
    octnum='regexp:0[0-7]*' // ok this was supposed to be '0[0-7]+' but we need to match '0' as well so we will do it here
    decnum='regexp:[1-9][0-9]*'
    id='regexp:[a-zA-Z_][a-zA-Z_0-9]*'

    strmultiline="regexp:'''(.|\n)*'''"
    strsimple="regexp:'(\\.|[^\\'])*'"

    block_comment="regexp:(\h*?#.*?\R+)(\h*?#.*?\R+)+"
    line_comment="regexp:#.*\R+"
  ]
//  name(".*_expression")='expression'
//  extends(".*_expression")=expression
}

build_definition ::= statement_list
statement_list ::= (statement | newline)*
for_statement_list ::= (for_statement | newline)*
statement ::= !<<eof>> (method_call_statement | func_call_statement | assignment_statement | selection_statement | repetitive_statement) {pin=2; recoverWhile=statement_recover}
private statement_recover ::= newline
for_statement ::= !<<eof>> (method_call_statement | func_call_statement | assignment_statement | selection_statement | repetitive_statement | control_statement) {pin=2; recoverWhile=statement_recover}
control_statement ::= ("continue" | "break") newline

// assignment statement
assignment_statement ::= id_expression atrop full_expression newline {pin=2}
atrop ::= "=" | "+=" | "-=" | "/=" | "*=" | "%="

// function call statement
func_call_expression ::= func_name "(" [func_args] ")" {pin=2}
func_call_statement ::= func_call_expression newline
func_name ::= id

// if "selection" statement
selection_statement ::= "if" full_bool_expression newline statement_list ("elif" full_bool_expression newline statement_list)* ["else" newline statement_list] "endif" newline {pin=1; recoverWhile=selection_statement_recover}
// this one is supposed to be in a foreach structure, so it should allow jump and break
selection_statement_for ::= "if" full_bool_expression newline statement_list ("elif" full_bool_expression newline statement_list)* ["else" newline statement_list] "endif" newline {pin=1; recoverWhile=selection_statement_recover}
private selection_statement_recover ::= !"endif" newline

repetitive_statement ::= "foreach" id_list ":" full_expression newline for_statement_list "endforeach" newline {pin=1; recoverWhile=repetitive_statement_recover}
private repetitive_statement_recover ::= !"endforeach" newline

func_args ::= newline? func_arg (newline? "," newline? func_arg)*(","? newline?)
func_arg ::= keyword_elem | positional_func_arg
positional_func_arg ::= full_expression
keyword_elem ::= id ":" full_expression

id_expression ::= id
id_list ::= id (newline? "," newline? id)*

method_call_expression_base ::= func_call_expression | id | literal
subscript_method_chain_expr ::= method_call_expression_base (newline? "." newline? func_call_expression | index_expr)+
method_call_statement ::= subscript_method_chain_expr newline

private index_expr ::= newline? "[" newline? add_expr newline?"]" {pin=2}

array_literal ::= "[" [newline? full_expression (newline? "," newline? full_expression)*] (","? newline?) "]"
dict_literal ::= "{" [newline? keyword_elem (newline? "," newline? keyword_elem)*] (","? newline?) "}"

mul_op ::= '*' | '/' | '%'
mul_expr ::= atom (newline? mul_op newline? atom)*

private add_op ::= '+' | '-'
add_expr ::= mul_expr (newline? add_op newline? mul_expr)*

private eq_op ::= '==' | '!='
relation_check_op ::= '>=' | '>' | '<=' | '<' | eq_op | "in" | "not" "in"
relation_check ::= add_expr newline? relation_check_op newline? add_expr {pin=3}

private bool_atom ::= bool_literal | relation_check | add_expr | par_expression | "not" bool_atom
conditional_and_expression ::= bool_atom (newline? "and" newline? bool_atom)*
conditional_or_expression ::= conditional_and_expression (newline? "or" newline? conditional_and_expression)*

atom ::= subscript_method_chain_expr | func_call_expression | par_expression | id | literal

ternary_expression ::= (conditional_or_expression | add_expr) newline? "?" newline? full_expression newline? ":" newline? full_expression {pin=3}

full_expression ::= ternary_expression | conditional_or_expression | add_expr
private par_expression ::= "(" newline? full_expression newline? ")"
private full_bool_expression ::= full_expression

// literals
private literal ::= dict_literal | array_literal | num_literal | string_literal | num_literal_unary | bool_literal
private string_literal ::= strmultiline | strsimple

num_literal_unary ::= unary_num_op num_literal
private num_literal ::= bin_literal | oct_literal | dec_literal | hex_literal
private bool_literal ::= "true" | "false"
private unary_num_op ::= "+" | "-"
private bin_literal ::= binnum
private oct_literal ::= octnum
private dec_literal ::= decnum
private hex_literal ::= hexnum